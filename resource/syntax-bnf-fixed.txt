program                      -> PROGRAM IS body ';'
body                         -> declarationClosure BEGIN statementClosure END
declarationClosure           -> declarationClosure declaration
                             -> 
statementClosure             -> statementClosure statement
                             ->
declaration                  -> VAR varDeclClosure
                             -> TYPE typeDeclClosure
                             -> PROCEDURE procedureDeclClosure
varDeclClosure               -> varDeclClosure varDecl
                             ->
typeDeclClosure              -> typeDeclClosure typeDecl
                             ->
procedureDeclClosure         -> procedureDeclClosure procedureDecl
                             ->
varDecl                      -> identifier IDClosure typenameOption ':=' expression ';'
IDClosure                    -> IDClosure ',' identifier
                             ->
typenameOption               -> ':' typename
                             -> 
typeDecl                     -> identifier IS type ';' 
procedureDecl                -> identifier formalParams typenameOption IS body ';'
typename                     -> identifier
type                         -> ARRAY OF typename
                             -> RECORD component componentClosure END
componentClosure             -> componentClosure component
                             -> 
component                    -> identifier ':' typename ';'
formalParams                 -> '(' fpSection fpSectionClosure ')' 
                             -> '(' ')' 
fpSectionClosure             -> fpSectionClosure ';' fpSection
                             ->
fpSection                    -> identifier IDClosure ':' typename
statement                    -> lvalue ':=' expression ';'
                             -> identifier actualParams ';'
                             -> READ '(' lvalue statementLvalueClosure ')' ';'
                             -> WRITE writeParams ';'
                             -> IF expression THEN statementClosure
                                statementElsifClosure
                                statementElseOption END ';'
                             -> WHILE expression DO statementClosure END ';'
                             -> LOOP statementClosure END ';'
                             -> FOR identifier ':=' expression TO expression statementByOption DO statementClosure END ';'
                             -> EXIT ';'
                             -> RETURN expressionOption ';'
statementLvalueClosure       -> statementLvalueClosure ',' lvalue
                             ->
statementElsifClosure        -> statementElsifClosure ELSIF expression THEN statementClosure
                             ->
statementElseOption          -> ELSE statementClosure
                             -> 
statementByOption            -> BY expression
                             -> 
writeParams                  -> '(' writeExpr writeParamsExprClosure ')'
                             -> '(' ')'
writeParamsExprClosure       -> writeParamsExprClosure ',' writeExpr
                             ->
writeExpr                    -> string
                             -> expression
expressionOption             -> expression
                             -> 
expression                   -> orOperand
orOperand                    -> orOperand OR andOperand
                             -> andOperand
andOperand                   -> andOperand AND relationship
relationship                 -> summand '>' summand
                             -> summand '<' summand
                             -> summand '=' summand
                             -> summand '>=' summand
                             -> summand '<=' summand
                             -> summand '<>' summand
                             -> summand
summand			                 -> summand '+' factor
                             -> summand '-' factor
                             -> factor
factor			                 -> factor '*' unary
                             -> factor '/' unary
                             -> factor DIV unary
                             -> factor MOD unary
                             -> unary
unary			                   -> '+' unary
                             -> '-' unary
                             -> NOT unary
                             -> term
term			                   -> number
                             -> lvalue
                             -> identifier actualParams // Procedure call
                             -> identifier recordInits
                             -> identifier arrayInits
                             -> '(' expression ')'
                             -> constant
lvalue                       -> identifier
                             -> lvalue '[' expression ']'
                             -> lvalue '.' identifier
actualParams                 -> '(' expression actualParamsExprClosure ')'
                             -> '(' ')'
actualParamsExprClosure      -> actualParamsExprClosure ',' expression
                             ->
recordInits                  -> '{' identifier ':=' expression recordInitsPairClosure '}'
recordInitsPairClosure       -> recordInitsPairClosure ';' identifier ':=' expression
                             -> 
arrayInits                   -> '[<' arrayInit arrayInitClosure '>]'
arrayInitClosure             -> arrayInitClosure ',' arrayInit
                             -> 
arrayInit                    -> expression
                             -> expression OF expression
number                       -> INTEGER | REAL
string                       -> STRING
identifier                   -> ID
constant		                 -> TRUE
                             -> FALSE
                             -> NIL